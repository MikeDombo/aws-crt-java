apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

preBuild.doFirst {
    println('Installing libcrypto prebuilt binaries')
    def exec = (project.file('fetch_libcrypto.sh').absolutePath).execute()
    exec.waitForProcessOutput(System.out, System.err)
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    ndkVersion "21.0.6113669"

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //testInstrumentationRunnerArguments clearPackageData: 'true'

        // testOptions {
        //     execution 'ANDROIDX_TEST_ORCHESTRATOR'
        // }

        consumerProguardFiles 'consumer-rules.pro'

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }

        externalNativeBuild {
            cmake {
                arguments "-DBUILD_DEPS=ON"
            }
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['../../src/main/java']
        }
        androidTest {
            setRoot '../../src/test'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "../../CMakeLists.txt"
            buildStagingDirectory "../../target/cmake-build"
            version "3.10.2"
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

build.dependsOn preBuild

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    //androidTestUtil 'androidx.test:orchestrator:1.2.0'
}
